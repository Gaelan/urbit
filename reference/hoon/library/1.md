volume 1, Hoon Structures
=======================

XX Introduction/overview of structures goes here.

---

###abel

    ++  abel  typo                                          ::  original sin: type

Used for changing `++type`, which contains twigs and consequently all rune structures

    ~zod/try=> *abel
    %void

--- 

###axis

    ++  axis  ,@                                            ::  tree address

A Nock axis, an address inside a Nock noun. In binary, after the leading 1,
a 1 signfies right, and 0, left.
   
    ~zod/try=> *axis
    0   

Note that 0 is not a valid axis.

    ~zod/try=> !=(+<)
    [0 6]
    ~zod/try=> +:!=(+<)
    6
    ~zod/try=> `@ub`(axis +:!=(+<))
    0b110

---

###also

    ++  also  ,[p=term q=wing r=type]                       ::  alias

XX unused?

---

###base

    ++  base  ?([%atom p=odor] %noun %cell %bean %null)     ::  axils, @ * ^ ? ~

The cases of an %axil. See %axil in `++tile`.

    ~zod/try=> *base
    %null
    ~zod/try=> p:!>(_@ud)
    [%atom p=%ud]

--- 

###beer

    ++  beer  $|(@ [~ p=twig])                              ::  simple embed

Used in `++phax` and `++scat` to build tapes, section 2fD

    ~zod/try=> `beer`'as'
    29.537
    ~zod/try=> (beer `(ream 'lan'))
    [~ p=[%cnzz p=~[%lan]]]


---

###beet

    ++  beet  $|  @                                         ::  advanced embed
              $%  [%a p=twig]                               ::  take tape
                  [%b p=twig]                               ::  take manx
                  [%c p=twig]                               ::  take marl
                  [%d p=twig]                               ::  take $+(marl marl)
                  [%e p=twig q=(list tuna)]                 ::  element literal
              ==                                            ::

Used in `++sail` xml interpolation, section 2fD

---

###bloq

    ++  bloq  ,@                                            ::  blockclass

An atom representing a blocksize, by convention expressed as a power of 2.

Used throughout section 2cA: bit surgery.

`++met` measures how many bloqs long an atom is. It takes a bloq
and an atom. In the below example, the 256 is 2 bloqs of 2^3: it takes two bytes
to represent the atom 256.

    ~zod/try=> (met 3 256) 
    2

---

###calf

    ++  calf  ,[p=(map ,@ud wine) q=wine]                   ::

Type encoding cyclical backreferences

Produced by `++dole`, consumed by `++doge` and `++dish`, section 2fC.

See [#wine]

---

###char

    ++  char  ,@tD                                          ::  UTF-8 byte

A single character. Odor @tD designates a single Unicode byte. 

Used in `++tape` string type, its parsing in chapter 2f:
`++lust`, `++just`, `++mask`, `++next`, and `++hit`

`++tape` [#tape], which is a string type, is a list of chars.

---

###chub

    ++  chub                                                ::  registered battery
              $:  p=(pair chum tyre)                        ::  definition
                  q=*                                       ::  battery
                  r=(unit (pair axis chub))                 ::  parent
              ==

Used (implicitly?) by compiler to attach jets, section 2fB.
Also used in :haus to make chat messages fly faster(???)

---

###chum

    ++  chum  $?  lef=term                                  ::  jet name
                  [std=term kel=@]                          ::  kelvin version
                  [ven=term pro=term kel=@]                 ::  vendor and product
                  [ven=term pro=term ver=@ kel=@]           ::  all of the above
              ==                                            ::

The jet hint information that must be present in the body of a ~/
or ~% rune.

Appears in `++twig` and `++clue`

A %fern of the following cases:

-  the jet's name
-  the jet's name and the kelvin number
-  the jet's vendor, name and kelvin number
-  the jet's vendor, name, legacy major.minor version and kelvin number

---

###claw

    ++  claw  
        XX  unused?

---

###clue

    ++  clue  ,[p=axis q=chum r=tyre]                       ::  battery definition

Used by compiler to attach jets, section 2fB  XX implementation does not currenlty match docs

---

###coat  

    ++  coat  ,[p=path q=vase]                              ::

Used nowhere, shadowed in 4aG XX

---

###coil  

    ++  coil  $:  p=?(%gold %iron %lead %zinc)              ::
                  q=type                                    ::
                  r=[p=?(~ ^) q=(map term foot)]            ::
              ==                                            ::

Core. XX

Variance, type, and arms XX

Generated somehwere in `++vast` XX
Converted by `++core` to %core twig

---

###coin  

    ++  coin  $%  [%$ p=dime]                               ::
                  [%blob p=*]                               ::
                  [%many p=(list coin)]                     ::
              ==                                            ::

Literal syntax primitive XX

Used to parse and print e.g. `0x2b59` or `~s1`, `++so` and `++co`
in section 2eL respectively

---


###cord  

    ++  cord  ,@t                                           ::  text atom (UTF-8)

One of Hoon's two string types (the other being `++tape`). A cord is an
atom of UTF-8 text.

Used everywhere XX
Notably, `++trip` and `++crip` convert between cord and `++tape`

Atom with odor. @t is a Unicode atom. The order of bytes in a @t are
little-endian, i.e. the first character in the text is the low byte of
the atom.

    ~zod/try=> `@ux`'foobar'
    0x7261.626f.6f66

---

###date  

    ++  date  ,[[a=? y=@ud] m=@ud t=tarp]                   ::  parsed date

A point in time. 

Converted to/from @d using `++year`/`++yore` in section 2cH,
Parsed/printed as UTC by `++stud`/`++dust` in section 3bc

A loobean designating AD or BC, a year atom, a month atom, and a `++tarp`, which
is a day atom and a time.

`++yell` produces a `++date` from a @da (a date atom)

    ~zod/try=> (yell ~2014.6.6..21.09.15..0a16)
    [d=106.751.991.820.172 h=21 m=9 s=15 f=~[0xa16]]

---

###dime  

    ++  dime  ,[p=@ta q=@]                                  ::

Odor-atom pair, used in `++so`/`++co`, section 2eL

---

### dram 

    ++  dram  $%  [| p=(map ,@tas dram)]                    ::  simple unix dir
                  [& p=@ud q=@]                             ::

The structure of a unix filesystem tree.

One of two cases: 

- `|` a directory - a map of names to deeper tree structures.
- `&` a file - a numbered atom of data.

Cards %dire and %pour in zuse require a `++dram` argument to target.

---

###each  

    ++  each  |*([a=$+(* *) b=$+(* *)] $%([& p=a] [| p=b])) ::

Dicriminated fork between two types XX

Used everywhere XX

---

###edge

    ++  edge  ,[p=hair q=(unit ,[p=* q=nail])]              ::  parsing output
    
Half-parsed tape with location metadata XX

Used in all section 2eD: combinators, and implicitly everywhere a `++rule` is used.

---

###foot

    ++  foot  $%  [%ash p=twig]                             ::  dry arm, geometric
                  [%elm p=twig]                             ::  wet arm, generic
                  [%oak ~]                                  ::  XX not used
                  [%yew p=(map term foot)]                  ::  XX not used
              ==                                            ::

Arm with wetness XX

Used in `++ap` to compile cores and `++ut` to type-check them, sections 2fB and 2fC

---

###gate

    ++  gate  $+(* *)                                       ::  general gate

Used everywhere XX

---

### gear  

    ++  gear  |*  a=_,*                                     ::  XX list generator
              $_                                            ::
              =|  b=*                                       ::
              |?                                            ::
              ?@  b                                         ::
                ~                                           ::
              [i=(a -.b) t=^?(..$(b +.b))]                  ::

Unused.
              1
---

###hair  

    ++  hair  ,[p=@ud q=@ud]                                ::  parsing trace

Line and column number.

Used to track position in parsing, `++last` in section 2eB

---

###hapt  

    ++  hapt  (list ,@ta)                                   ::  XX not used

---

###like  

    ++  like  |*  a=_,*                                     ::  generic edge
              |=  b=_`*`[(hair) ~]                          ::
              :-  p=(hair -.b)                              ::
              ^=  q                                         ::
              ?@  +.b  ~                                    ::
              :-  ~                                         ::
              u=[p=(a +>-.b) q=[p=(hair -.b) q=(tape +.b)]] ::
              
Type to "parser to that type" type XX

Used for funky vulcan stuff: primitives `++easy`, `++just`/`++jest`, `++knee`, `++mask`,
`++shim`, `++stir`, `++stun` in section 2eC

---

###limb  

    ++  limb  $|(term $%([%& p=axis] [%| p=@ud q=term]))    ::
    
Reference into subject by name/axis

Used in compilation and grammar, section 2fC-2fD

---

###line  

    ++  line  ,[p=[%leaf p=odor q=@] q=tile]                ::  %kelp case
    
Dicriminated union unionee XX

Used in compilation and grammar, section 2fC-2fD

---

###list  

    ++  list  |*  a=_,*                                     ::  null-term list
              $|(~ [i=a t=(list a)])                        ::

Used everywhere XX
Frequently through the helper gates in section 2bB

---

###lone

    ++  lone  |*(a=$+(* *) ,p=a)                            ::  just one thing

Used nowhere XX

---

###mane  

    ++  mane  $|(@tas [@tas @tas])                          ::  XML name/space

Parsed by `++name`:xmlp, rendered by `++xmln`, section 3bD

---

###manx  

    ++  manx  ,[g=marx c=marl]                              ::  XML node

Top level xml node.

Parsed by `++apex`:xmlp, rendered by `++xmlt`, section 3bD

---

###marl  

    ++  marl  (list manx)                                   ::  XML node list

Parsed within `++apex`:xmlp, rendered by `++xmll`, section 3bD

---

###mars  

    ++  mars  ,[t=[n=%$ a=[i=[n=%$ v=tape] t=~]] c=~]       ::  XML cdata

,_:/(tape), used nowhere XX should be in `++chrd`:xmlp

Is a valid `++manx`

---

###mart  

    ++  mart  (list ,[n=mane v=tape])                       ::  XML attributes

Parsed by `++attr`:xmlp, rendered by `++xmla`, section 3bD

---

###marx  

    ++  marx  ,[n=mane a=mart]                              ::  XML tag

Parsed by `++head`:xmlp, rendered within `++xmlt`, section 3bD

---

###metl  

    ++  metl  ?(%gold %iron %zinc %lead)                    ::  core variance

Used implicitly in `++coil`

---

###noun

    ++  noun  ,*                                            ::  any noun

Used nowhere XX

---

###null  

    ++  null  ,~                                            ::  null, nil, etc

Used nowhere XX

---

###odor  

    ++  odor  ,@ta                                          ::  atom format

Used in `++base`

---

###tarp  

    ++  tarp  ,[d=@ud h=@ud m=@ud s=@ud f=(list ,@ux)]      ::  parsed time

Used in `++date`, converted from and to @d by `++yell`/`++yule` in section 2cH

---

###time  

    ++  time  ,@da                                          ::  galactic time

---

###tree  

    ++  tree  |*  a=_,*                                     ::  binary tree
              $|(~ [n=a l=(tree a) r=(tree a)])             ::
---

###nail  

    ++  nail  ,[p=hair q=tape]                              ::  parsing input

---

###numb  

    ++  numb  ,@                                            ::  just a number

---

###pair  

    ++  pair  |*([a=$+(* *) b=$+(* *)] ,[p=a q=b])          ::  just a pair

---

###pass  

    ++  pass  ,@                                            ::  public key

---

###path  

    ++  path  (list span)                                   ::  filesys location

---

###pint  

    ++  pint  ,[p=[p=@ q=@] q=[p=@ q=@]]                    ::  line/column range

---

###pole

    ++  pole  |*  a=_,*                                     ::  nameless list
              $|(~ [a (pole a)])                            ::
---

###port

    ++  port  $:  p=axis                                    ::
                  $=  q                                     ::
                  $%  [%& p=type]                           ::
                      [%| p=axis q=(list ,[p=type q=foot])] ::
                  ==                                        ::
              ==                                            ::

---

###post  

    ++  post  $:  p=axis                                    ::
                  $=  q                                     ::
                  $%  [0 p=type]                            ::
                      [1 p=axis q=(list ,[p=type q=foot])]  ::
                      [2 p=twin q=type]                     ::
                  ==                                        ::
              ==                                            ::
---

###prop  

    ++  prop  $:  p=axis                                    ::
                  $=  q                                     ::
                  [p=?(~ axis) q=(list ,[p=type q=foot])]   ::
              ==                                            ::
---

###qual  

    ++  qual  |*  [a=$+(* *) b=$+(* *) c=$+(* *) d=$+(* *)] ::  just a quadruple
              ,[p=a q=b r=c s=d]                            ::
---

###rege  

    ++  rege  $|  ?(%dote %ende %sart %empt %boun %bout)    ::  parsed regex
              $%  [%lite p=char]                            ::  literal
                  [%pair p=rege q=rege]                     ::  ordering
                  [%capt p=rege q=@u]                       ::  capture group
                  [%brac p=@]                               ::  p is 256 bitmask
                  [%eith p=rege q=rege]                     ::  either
                  [%mant p=rege]                            ::  greedy 0 or more
                  [%plls p=rege]                            ::  greedy 1 or more
                  [%betw p=rege q=@u r=@u]                  ::  between q and r
                  [%bint p=rege q=@u]                       ::  min q
                  [%bant p=rege q=@u]                       ::  exactly q
                  [%manl p=rege]                            ::  lazy 0 or more
                  [%plll p=rege]                            ::  lazy 1 or more
                  [%betl p=rege q=@u r=@u]                  ::  between q and r lazy
                  [%binl p=rege q=@u]                       ::  min q lazy
              ==

---

###ring  

    ++  ring  ,@                                            ::  private key

---

###rule

    ++  rule  |=(tub=nail `edge`[p.tub ~ ~ tub])            ::  parsing rule

---

###span

    ++  span  ,@ta                                          ::  text-atom (ASCII)

A restricted text atom for canonical atom syntaxes. The prefix is `~.`.
There are no escape sequences except `~~`, which means `~`, and `~-`,
which means `_`. - and .  encode themselves. No other characters
besides numbers and lowercase letters are permitted.

    ~zod/try=> `@t`~.foo
    'foo'
    ~zod/try=> `@t`~.foo.bar
    'foo.bar'
    ~zod/try=> `@t`~.foo~~bar
    'foo~bar'
    ~zod/try=> `@t`~.foo~-bar
    'foo_bar'
    ~zod/try=> `@t`~.foo-bar
    'foo-bar'

---

###spot

    ++  spot  ,[p=path q=pint]                              ::  range in file

---

###tang

    ++  tang  (list tank)                                   ::  general error

---

###tank

    ++  tank  $%  [%leaf p=tape]                            ::  printing formats
                  [%palm p=[p=tape q=tape r=tape s=tape] q=(list tank)]
                  [%rose p=[p=tape q=tape r=tape] q=(list tank)]
              ==                                            ::
---

###tape

    ++  tape  (list char)                                   ::  like a string

One of Hoon's two string types (the other being `++cord`). A tape is a
list of chars.

    ~zod/try=> `(list ,char)`"foobar"
    "foobar"
    ~zod/try=> `(list ,@)`"foobar"
    ~[102 111 111 98 97 114]

---

###term

A restricted text atom for Hoon constants. The only characters
permitted are lowercase ASCII, - except as the first or last character,
and 0-9 except as the first character.

The syntax for @tas is the text itself, always preceded by %. This
means a term is always cubical. You can cast it to @tas if you like,
but we just about always want the cube:

    ~zod/try=> %dead-fish9
    %dead-fish9
    ~zod/try=> -:!>(%dead-fish9)
    [%cube p=271.101.667.197.767.630.546.276 q=[%atom p=%tas]]

The empty @tas has a special syntax, $:

    ~zod/try=> %$
    %$
    
A term without % is not a constant, but a name:

    ~zod/try=> dead-fish9
    ! -find-limb.dead-fish9
    ! find-none
    ! exit

---

###tiki

    ++  tiki

---

###tile

    ++  tile  

See the Tile section of the Hoon Reference.

---

###toga

    ++  toga                                                ::  face control
              $|  p=term                                    ::  two togas
              $%  [0 ~]                                     ::  no toga
                  [1 p=term q=toga]                         ::  deep toga
                  [2 p=toga q=toga]                         ::  cell toga
              ==                                            ::

---

###trap

    ++  trap  ,_|.(_*)                                      ::  makes perfect sense

---

###trel

    ++  trel  |*  [a=$+(* *) b=$+(* *) c=$+(* *)]           ::  just a triple
              ,[p=a q=b r=c]                                ::

---

###tuna

++  tuna  
    
An XML template tree.  

Leaf %a contains plain-text, %b an empty tag, %c a static list, %d a 
dynamic list, %e a full node element containing a twig and a list of
tuna, and %f is a empty node.

---

###twig

++  twig

See Twig section of Hoon reference

---

###tine

    ++  tine  (list ,[p=tile q=twig])                       ::

---

###tusk

    ++  tusk  (list twig)                                   ::

---

###tyre

    ++  tyre  (list ,[p=term q=twig])                       ::

---

###tyke

    ++  tyke  (list (unit twig))                            ::

---

###tram

    ++  tram  (list ,[p=wing q=twig])                       ::

---

###tone

    ++  tone  $%  [%0 p=*]                                  ::
                  [%1 p=(list)]                             ::
                  [%2 p=(list ,[@ta *])]                    ::
              ==                                            ::

---

###nock

    ++  nock  $&  [p=nock q=nock]                           ::  autocons
              $%  [%0 p=@]                                  ::  axis select
                  [%1 p=*]                                  ::  constant
                  [%2 p=nock q=nock]                        ::  compose
                  [%3 p=nock]                               ::  cell test
                  [%4 p=nock]                               ::  increment
                  [%5 p=nock q=nock]                        ::  equality test
                  [%6 p=nock q=nock r=nock]                 ::  if, then, else
                  [%7 p=nock q=nock]                        ::  serial compose
                  [%8 p=nock q=nock]                        ::  push onto subject
                  [%9 p=@ q=nock]                           ::  select arm and fire
                  [%10 p=?(@ [p=@ q=nock]) q=nock]          ::  hint
                  [%11 p=nock]                              ::  grab data from sky
              ==                                            ::

---

###toon

    ++  toon  $%  [%0 p=*]                                  ::
                  [%1 p=(list)]                             ::
                  [%2 p=(list tank)]                        ::
              ==                                            ::

---

###tune

    ++  tune  $%  [%0 p=vase]                               ::
                  [%1 p=(list)]                             ::
                  [%2 p=(list ,[@ta *])]                    ::
              ==                                            ::

---

###twin

    ++  twin  ,[p=term q=wing r=axis s=type]                ::

---

###type

See Type section of Hoon reference

---

###typo

Deprecated type name maintained for compatability.

    ++  typo  type                                          ::  old type

---

###udal

    ++  udal                                                ::  atomic change (%b)
              $:  p=@ud                                     ::  blockwidth
                  q=(list ,[p=@ud q=(unit ,[p=@ q=@])])     ::  indels
              ==                                            ::

---

###udon

    ++  udon                                                ::  abstract delta
              $:  p=umph                                    ::  preprocessor
                  $=  q                                     ::  patch
                  $%  [%a p=* q=*]                          ::  trivial replace
                      [%b p=udal]                           ::  atomic indel
                      [%c p=(urge)]                         ::  list indel
                      [%d p=upas q=upas]                    ::  tree edit
                  ==                                        ::
              ==                                            ::

---

###umph

    ++  umph                                                ::  change filter
              $|  $?  %a                                    ::  no filter
                      %b                                    ::  jamfile
                      %c                                    ::  LF text
                  ==                                        ::
              $%  [%d p=@ud]                                ::  blocklist
              ==                                            ::
    
---

###unce

    ++  unce  |*  a=_,*                                     ::  change part
              $%  [%& p=@ud]                                ::  skip[copy]
                  [%| p=(list a) q=(list a)]                ::  p -> q[chunk]
              ==                                            ::

---

###unit

    ++  unit  |*  a=_,*                                     ::  maybe
              $|(~ [~ u=a])                                 ::

---

###upas

    ++  upas                                                ::  tree change (%d)
              $&  [p=upas q=upas]                           ::  cell
              $%  [%0 p=axis]                               ::  copy old
                  [%1 p=*]                                  ::  insert new
                  [%2 p=axis q=udon]                        ::  mutate!
              ==                                            ::

---

###urge

    ++  urge  |*(a=_,* (list (unce a)))                     ::  list change

---

###vase

    ++  vase  ,[p=type q=*]                                 ::  type-value pair

---

###vise

    ++  vise  ,[p=typo q=*]                                 ::  old vase

---

###wall

    ++  wall  (list tape)                                   ::  text lines (no \n)

---

###wain

    ++  wain  (list cord)                                   ::  text lines (no \n)

---

###wing

    ++  wing  (list limb)                                   ::

---

###wine

    ++  wine  $|  ?(%noun %path %tank %void %wall %wool %yarn)
              $%  [%atom p=term]                            ::
                  [%core p=(list ,@ta) q=wine]              ::
                  [%face p=term q=wine]                     ::
                  [%list p=term q=wine]                     ::
                  [%pear p=term q=@]                        ::
                  [%pick p=(list wine)]                     ::
                  [%plot p=(list wine)]                     ::
                  [%stop p=@ud]                             ::
                  [%tree p=term q=wine]                     ::
                  [%unit p=term q=wine]                     ::
              ==                                            ::

---

###wonk

    ++  wonk  |*(veq=edge ?~(q.veq !! p.u.q.veq))           ::

---

###map

    ++  map  |*  [a=_,* b=_,*]                              ::  associative array
             $|(~ [n=[p=a q=b] l=(map a b) r=(map a b)])    ::

---

###qeu

    ++  qeu  |*  a=_,*                                      ::  queue
             $|(~ [n=a l=(qeu a) r=(qeu a)])                ::

---

###set

    ++  set  |*  a=_,*                                      ::  set
             $|(~ [n=a l=(set a) r=(set a)])                ::

---

###jar

    ++  jar  |*([a=_,* b=_,*] (map a (list b)))             ::  map of lists

---

###jug

    ++  jug  |*([a=_,* b=_,*] (map a (set b)))              ::  map of sets

---
