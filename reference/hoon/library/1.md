volume 1, Hoon Structures
=======================

XX Introduction/overview of structures goes here.

---

###abel

    ++  abel  typo                                          ::  original sin: type

Used for changing `++type`, which contains twigs and consequently all rune structures

    ~zod/try=> *abel
    %void

--- 

###axis

    ++  axis  ,@                                            ::  tree address

A Nock axis, an address inside a Nock noun. In binary, after the leading 1,
a 1 signfies right, and 0, left.

Used in `++clue`, `++limb`

Consumed by `++slew`, `++slot`, and section 2fC
   
    ~zod/try=> *axis
    0  
    ~zod/try=> :: not actually a valid axis
    
    ~zod/try=> !=(+<)
    [0 6]
    ~zod/try=> +:!=(+<)
    6
    ~zod/try=> `@ub`(axis +:!=(+<))
    0b110

---

###also

    ++  also  ,[p=term q=wing r=type]                       ::  alias

XX unused?

---

###base

    ++  base  ?([%atom p=odor] %noun %cell %bean %null)     ::  axils, @ * ^ ? ~

The cases of an %axil. See %axil in `++tile`.

    ~zod/try=> *base
    %null
    
    ~zod/try=> p:!>(_@ud)
    [%atom p=%ud]

--- 

###bean

    ++  bean  ,?                                            ::  0=&=yes, 1=|=no
    
Loobean: yes or no.

    ~zod/try=> `bean`&
    %.y

---

###beer

    ++  beer  $|(@ [~ p=twig])                              ::  simple embed

Used in `++phax` and `++scat` to build tapes, section 2fD

    ~zod/try=> `beer`'as'
    29.537
    
    ~zod/try=> (beer `(ream 'lan'))
    [~ p=[%cnzz p=~[%lan]]]


---

###beet

    ++  beet  $|  @                                         ::  advanced embed
              $%  [%a p=twig]                               ::  take tape
                  [%b p=twig]                               ::  take manx
                  [%c p=twig]                               ::  take marl
                  [%d p=twig]                               ::  take $+(marl marl)
                  [%e p=twig q=(list tuna)]                 ::  element literal
              ==                                            ::

Used in `++sail` xml interpolation, section 2fD

---

###bloq

    ++  bloq  ,@                                            ::  blockclass

An atom representing a blocksize, by convention expressed as a power of 2.

Used throughout section 2cA: bit surgery.

`++met` measures how many bloqs long an atom is. It takes a bloq
and an atom. In the below example, the 256 is 2 bloqs of 2^3: it takes two bytes
to represent the atom 256.

    ~zod/try=> (met 3 256) 
    2

---

###calf

    ++  calf  ,[p=(map ,@ud wine) q=wine]                   ::

Type encoding cyclical backreferences

Produced by `++dole`, consumed by `++doge` and `++dish`, section 2fC.

See [#wine]

    ~zod/try=> `calf`[~ %atom %ta]
    [p={} q=[%atom p=%ta]]
    
    ~zod/try=> ~(dole ut p:!>(*^))
    [p={} q=[%plot p=~[%noun %noun]]]
    
    ~zod/try=> ~(dole ut p:!>($:|-(?(~ [* $]))))
    [ p={[p=1 q=[%pick p=~[[%pear p=%n q=0] [%plot p=~[%noun [%stop p=1]]]]]]}
      q=[%stop p=1]
    ]

---

###char

    ++  char  ,@tD                                          ::  UTF-8 byte

A single character. Odor `@tD` designates a single Unicode byte. 

Used in `++tape` string type, parsed in chapter 2f by
`++lust`, `++just`, `++mask`, `++next`, and `++hit`.

`++tape` [#tape], which is a string type, is a list of chars.

---

###chub

    ++  chub                                                ::  registered battery
              $:  p=(pair chum tyre)                        ::  definition
                  q=*                                       ::  battery
                  r=(unit (pair axis chub))                 ::  parent
              ==

Used (implicitly?) by compiler to attach jets, section 2fB.

---

###chum

    ++  chum  $?  lef=term                                  ::  jet name
                  [std=term kel=@]                          ::  kelvin version
                  [ven=term pro=term kel=@]                 ::  vendor and product
                  [ven=term pro=term ver=@ kel=@]           ::  all of the above
              ==                                            ::

The jet hint information that must be present in the body of a ~/
or ~% rune. Can optionally contain a kelvin version, jet vendor, and major.minor
version number.

XX shadowed by zuse

Appears in `++twig` and `++clue`

    ~zod/try=> `chum`'hi'
    lef=%hi
    
    ~zod/try=> (ream '~/(%lob.314 !!)')
    [%sgfs p=[std=%lob kel=314] q=[%zpzp ~]]
    

---

###clue

    ++  clue  ,[p=axis q=chum r=tyre]                       ::  battery definition

Used by compiler to attach jets, section 2fB  XX implementation does not currenlty match docs

---

###coil  

    ++  coil  $:  p=?(%gold %iron %lead %zinc)              ::  core type
                  q=type                                    ::
                  r=[p=?(~ ^) q=(map term foot)]            ::
              ==                                            ::

Core: variance, subject type, optional compiled nock, and arms.

Used as an intermediate step within section 2fB

Converted by `++core` to %core type.
   
---

###coin  

    ++  coin  $%  [%$ p=dime]                               ::
                  [%blob p=*]                               ::
                  [%many p=(list coin)]                     ::
              ==                                            ::

Literal syntax, coming in three forms: atoms, jammed nouns, and nestable tuples.

Parsed and printed using `++so` and `++co` cores in section 2eL respectively

    ~zod/try=> `coin`(need (slay '~s1'))
    [%$ p=[p=~.dr q=18.446.744.073.709.551.616]]
    ~zod/try=> `coin`(need (slay '0x2b59'))
    [%$ p=[p=~.ux q=11.097]]
    
    ~zod/try=> ~(rend co [%many ~[[%$ %ud 1] [%$ %tas 'a'] [%$ %s -2]]])
    "._1_a_-2__"
    ~zod/try=> ._1_a_-2__
    [1 %a -2]

    ~zod/try=> `@uv`(jam [3 4])
    0v2cd1
    ~zod/try=> (slay '~02cd1')
    [~ [%blob p=[3 4]]]
    ~zod/try=> ~02cd1
    [3 4]

---


###cord  

    ++  cord  ,@t                                           ::  text atom (UTF-8)

One of Hoon's two string types (the other being `++tape`). A cord is an
atom of UTF-8 text.

`++trip` and `++crip` convert between cord and `++tape`

Odor `@t` designates a Unicode atom, little-endian: the first character in the
text is the low byte.

    ~zod/try=> `@ux`'foobar'
    0x7261.626f.6f66

---

###date  

    ++  date  ,[[a=? y=@ud] m=@ud t=tarp]                   ::  parsed date

A point in time. 

Converted to/from `time` using `++year`/`++yore` in section 2cH,
Parsed/printed as UTC by `++stud`/`++dust` in section 3bc

A loobean designating AD or BC, a year atom, a month atom, and a `++tarp`, which
is a day atom and a time.

    ~zod/try=> (yore ~2014.6.6..21.09.15..0a16)
    [[a=%.y y=2.014] m=6 t=[d=6 h=21 m=9 s=15 f=~[0xa16]]]

---

###dime  

    ++  dime  ,[p=odor q=@]                                  ::

Odor-atom pair, used in `++coin`, operated on by `++so`/`++co` in section 2eL

    ~zod/try=> +>:(slay '0x123')
    p=[p=~.ux q=291]

Convenience methods `++scot` and `++scow` print dimes as cords/tapes, `++slat`,
`++slav`, and `++slaw` are used to parse atoms of specific odor.

---

###dram 

    ++  dram  $%  [| p=(map ,@tas dram)]                    ::  simple unix dir
                  [& p=@ud q=@]                             ::

The structure of a unix filesystem tree.

One of two cases: 

- `|` a directory - a map of names to deeper tree structures.
- `&` a file - a numbered atom of data.

Unused XX

---

###each  

    ++  each  |*([a=$+(* *) b=$+(* *)] $%([& p=a] [| p=b])) ::

Tile generator: dicriminated fork between two types.

    ~zod/try=> :type; *(each cord time)
    [%.y p='']
    {[%.y p=@t] [%.n p=@da]}

---

###edge

    ++  edge  ,[p=hair q=(unit ,[p=* q=nail])]              ::  parsing output
    
Location metadata, optional result and parsing continuation.

Used in all section 2eD: combinators, and implicitly everywhere a `++rule` is used.

    ~zod/try=> *edge
    [p=[p=0 q=0] q=~]
    ~zod/try=> (tall:vast [1 1] "a b")
    [p=[p=1 q=2] q=[~ [p=[%cnzz p=~[%a]] q=[p=[p=1 q=2] q=" b"]]]]

---

###foot

    ++  foot  $%  [%ash p=twig]                             ::  dry arm, geometric
                  [%elm p=twig]                             ::  wet arm, generic
                  [%oak ~]                                  ::  XX not used
                  [%yew p=(map term foot)]                  ::  XX not used
              ==                                            ::

Arm with variance model.

Used in `++ap` to compile cores and `++ut` to type-check them, sections 2fB and 2fC

     ~zod/try=> *foot
    [%yew p={}]
    
    ~zod/try=> (ream '|%  ++  $  foo  --')
    [%brcn p={[p=%$ q=[%ash p=[%cnzz p=~[%foo]]]]}]
    ~zod/try=> +<+:(ream '|%  ++  $  foo  --')
    t=~[%ash %cnzz %foo]
    ~zod/try=> (foot +<+:(ream '|%  ++  $  foo  --'))
    [%ash p=[%cnzz p=~[%foo]]]

---

###gate

    ++  gate  $+(* *)                                       ::  general gate

An core with one arm `$`, which transforms a sample noun into a product noun.

Used in higher-order gates (functions) such as `++lift` and `++cork`. If used
dryly as a type, subject must have a sample type of `*`.
    
    ~zod/try=> `gate`|=(* 0)
    <1|mws [* <101.jzo 1.ypj %164>]>
    
    ~zod/try=> (|=(a=* [a 'b']) '1')
    [49 'b']
    ~zod/try=> (`gate`|=(a=* [a 'b']) '1')
    [49 98]

---

###hair  

    ++  hair  ,[p=@ud q=@ud]                                ::  parsing trace

Line and column number.

Used to track position in parsing, `++last` in section 2eB

---

###like  

    ++  like  |*  a=_,*                                     ::  generic edge
              |=  b=_`*`[(hair) ~]                          ::
              :-  p=(hair -.b)                              ::
              ^=  q                                         ::
              ?@  +.b  ~                                    ::
              :-  ~                                         ::
              u=[p=(a +>-.b) q=[p=(hair -.b) q=(tape +.b)]] ::
              
Type to "parse result of that type" type XX

Utilized in vulcan funk: primitives `++easy`, `++just`/`++jest`, `++knee`,
`++mask`, `++shim`, `++stir`, `++stun` in section 2eC


    ~zod/try=> `(like path)`[[1 1] [~ "abc" [1 4] "..."]]
    [p=[p=1 q=1] q=[~ [p=/a/b/c q=[p=[p=1 q=4] q="..."]]]]
    
---

###limb  

    ++  limb  $|(term $%([%& p=axis] [%| p=@ud q=term]))    ::
    
Reference into subject by name/axis

Used in compilation and grammar, section 2fC-2fD

    ~zod/try=> (ream '^^$')
    [%cnzz p=~[[%.n p=2 q=%$]]]
    ~zod/try=> (limb &2:(ream '^^$'))
    [%.n p=2 q=%$]
    ~zod/try=> (limb &2:(ream '^^^$'))
    [%.n p=3 q=%$]

---

###line  

    ++  line  ,[p=[%leaf p=odor q=@] q=tile]                ::  %kelp case
    
Dicriminated union unionee XX

Used in compilation and grammar, section 2fC-2fD
  
    ~zod/try=> (ream '$%([1 a] [%2 b])')
    [ %bccm
        p
      [ %kelp
          p
        [ i=[p=[%leaf p=~.ud q=1] q=[%herb p=[%cnzz p=~[%a]]]]
          t=~[[p=[%leaf p=~.ud q=2] q=[%herb p=[%cnzz p=~[%b]]]]]
        ]
      ]
    ]
    ~zod/try=> &3:(ream '$%([1 a] [%2 b])')
    p=[p=[%leaf p=%ud q=1] q=[%herb p=[%cnzz p=~[%a]]]]
    ~zod/try=> (line &3:(ream '$%([1 a] [%2 b])'))
    [p=[%leaf p=~.ud q=1] q=[%herb p=[%cnzz p=~[%a]]]]

---

###list  

    ++  list  |*  a=_,*                                     ::  null-term list
              $|(~ [i=a t=(list a)])                        ::

Tile generator: null-terminated list of homogenous type.

Helper gates such as `++turn` and `++snag` are found in section 2bB.

    ~zod/try=> *(list)
    ~
    ~zod/try=> `(list ,@)`"abc"
    ~[97 98 99]
    ~zod/try=> (snag 0 "abc")
    ~~a

---

###lone

    ++  lone  |*(a=$+(* *) ,p=a)                            ::  just one thing

Tile generator: put face of `p` on something. Relatively useless

---

###mane  

    ++  mane  $|(@tas [@tas @tas])                          ::  XML name/space

Parsed by `++name`:xmlp, rendered by `++xmln`, section 3bD

See  `++sail` doc

---

###manx  

    ++  manx  ,[g=marx c=marl]                              ::  XML node

Top level xml node.

Parsed by `++apex`:xmlp, rendered by `++xmlt`, section 3bD

See  `++sail` doc

---

###marl  

    ++  marl  (list manx)                                   ::  XML node list

Parsed within `++apex`:xmlp, rendered by `++xmll`, section 3bD

See  `++sail` doc

---

###mars  

    ++  mars  ,[t=[n=%$ a=[i=[n=%$ v=tape] t=~]] c=~]       ::  XML cdata

,_:/(tape), used nowhere XX should be in `++chrd`:xmlp

Is a valid `++manx`

See  `++sail` doc

---

###mart  

    ++  mart  (list ,[n=mane v=tape])                       ::  XML attributes

Parsed by `++attr`:xmlp, rendered by `++xmla`, section 3bD

See  `++sail` doc

---

###marx  

    ++  marx  ,[n=mane a=mart]                              ::  XML tag

Parsed by `++head`:xmlp, rendered within `++xmlt`, section 3bD

See  `++sail` doc

---

###metl  

    ++  metl  ?(%gold %iron %zinc %lead)                    ::  core variance

Used implicitly in `++coil`

---

###noun

    ++  noun  ,*                                            ::  any noun

Used nowhere XX

    ~zod/try=> `noun`~[1 2 3]
    [1 2 3 0]

---

###null  

    ++  null  ,~                                            ::  null, nil, etc

Used nowhere XX

    ~zod/try=> :type; *null
    ~
    %~

---

###odor  

    ++  odor  ,@ta                                          ::  atom format

Used in `++base`

    ~zod/try=> `odor`%ux
    ~.ux

---

###tarp  

    ++  tarp  ,[d=@ud h=@ud m=@ud s=@ud f=(list ,@ux)]      ::  parsed time

Used in `++date`, converted from and to `@d` by `++yell`/`++yule` in section 2cH

    ~zod/try=> -<-
    ~2014.9.20..00.43.33..b52a
    ~zod/try=> (yell -<-)
    [d=106.751.991.820.278 h=0 m=43 s=39 f=~[0x54d1]]
    
    ~zod/try=> (yell ~d20)
    [d=20 h=0 m=0 s=0 f=~]

---

###time  

    ++  time  ,@da                                          ::  galactic time
    
The `@da` odor designates an absolute date atom

---

###tree  

    ++  tree  |*  a=_,*                                     ::  binary tree
              $|(~ [n=a l=(tree a) r=(tree a)])             ::
              
Tile generator: a tree can be empty, or contain a node of a type and left/right
subtrees of the same type. Pretty-printed with `{}`.

    ~zod/try=> `(tree ,[@ tape])`[[1 "hi"] [[2 "bye"] ~ ~] ~]
    {[2 "bye"] [1 "hi"]}


---

###nail  

    ++  nail  ,[p=hair q=tape]                              ::  parsing input

Location in text, and remainder of it.

    ~zod/try=> +<:;~(plug cab cab)
    c=tub=[p=[p=0 q=0] q=""]

---

###numb  

    ++  numb  ,@                                            ::  just a number

Used nowhere XX

---

###pair  

    ++  pair  |*([a=$+(* *) b=$+(* *)] ,[p=a q=b])          ::  just a pair

Tile generator: tuple of two

    ~zod/try=> *(pair bean cord)
    [p=%.y q='']

---

###pass  

    ++  pass  ,@                                            ::  public key
    
Atom alias, used in `++acru` cryptosuite type, and its implementations
`crua` and `crub`.

---

###path  

    ++  path  (list span)                                   ::  filesys location
    
Used in `%clay`, `%eyre`

    ~zod/try=> `path`"abc"
    /a/b/c

---

###pint  

    ++  pint  ,[p=[p=@ q=@] q=[p=@ q=@]]                    ::  line/column range
    
Pair of hairs, from - to
  
    ~zod/try=> !:(!!)
    ! /~zod/try/~2014.9.20..01.22.04..52e3/:<[1 4].[1 6]>
    ~zod/try=> `pint`[[1 4] [1 6]]
    [p=[p=1 q=4] q=[p=1 q=6]]

---

###pole

    ++  pole  |*  a=_,*                                     ::  nameless list
              $|(~ [a (pole a)])                            ::
              
Sometimes list faces aren't available
  
    ~zod/try=> `(pole char)`"asdf"
    [~~a [~~s [~~d [~~f ~]]]]

---

###port

    ++  port  $:  p=axis                                    ::
                  $=  q                                     ::
                  $%  [%& p=type]                           ::
                      [%| p=axis q=(list ,[p=type q=foot])] ::
                  ==                                        ::
              ==                                            ::
  
Type and location of core-shaped thing? XX

    ~zod/try=> *port
    [p=0 q=[%.y p=%void]]

---



###post  

    ++  post  $:  p=axis                                    ::
                  $=  q                                     ::
                  $%  [0 p=type]                            ::
                      [1 p=axis q=(list ,[p=type q=foot])]  ::
                      [2 p=twin q=type]                     ::
                  ==                                        ::
              ==                                            ::
              
Type and location of possibly core-shaped thing? XX

    ~zod/try=> *post
    [p=0 q=[%0 p=%void]]

---

###prop  

    ++  prop  $:  p=axis                                    ::
                  $=  q                                     ::
                  [p=?(~ axis) q=(list ,[p=type q=foot])]   ::
              ==                                            ::
              
Verified core-shaped thing? XX

    ~zod/try=> *prop
    [p=0 q=[p=~ q=~]]

---

###qual  

    ++  qual  |*  [a=$+(* *) b=$+(* *) c=$+(* *) d=$+(* *)] ::  just a quadruple
              ,[p=a q=b r=c s=d]                            ::
              
Tile constructor: tuple of 4

    ~zod/try=> *(qual date time tape cord)
    [p=[[a=%.y y=0] m=0 t=[d=0 h=0 m=0 s=0 f=~]] q=~292277024401-.1.1 r="" s='']

---

###rege  

    ++  rege  $|  ?(%dote %ende %sart %empt %boun %bout)    ::  parsed regex
              $%  [%lite p=char]                            ::  literal
                  [%pair p=rege q=rege]                     ::  ordering
                  [%capt p=rege q=@u]                       ::  capture group
                  [%brac p=@]                               ::  p is 256 bitmask
                  [%eith p=rege q=rege]                     ::  either
                  [%mant p=rege]                            ::  greedy 0 or more
                  [%plls p=rege]                            ::  greedy 1 or more
                  [%betw p=rege q=@u r=@u]                  ::  between q and r
                  [%bint p=rege q=@u]                       ::  min q
                  [%bant p=rege q=@u]                       ::  exactly q
                  [%manl p=rege]                            ::  lazy 0 or more
                  [%plll p=rege]                            ::  lazy 1 or more
                  [%betl p=rege q=@u r=@u]                  ::  between q and r lazy
                  [%binl p=rege q=@u]                       ::  min q lazy
              ==

Regex, names need restructuring XX

---

###ring  

    ++  ring  ,@                                            ::  private key
    
Atom alias, used in `++acru` cryptosuite type, and its implementations
`crua` and `crub`.



---

###rule

    ++  rule  |=(tub=nail `edge`[p.tub ~ ~ tub])            ::  parsing rule
    
Parser typeclass.

    ~zod/try=> ^+(rule [|=(a=nail [p.a ~])]:|6)  ::  wet gate needs same context
    <1.dww [tub=[p=[p=@ud q=@ud] q=""] <101.jzo 1.ypj %164>]>

---

###span

    ++  span  ,@ta                                          ::  text-atom (ASCII)

A restricted text atom for canonical atom syntaxes. The prefix is `~.`.
There are no escape sequences except `~~`, which means `~`, and `~-`,
which means `_`. - and .  encode themselves. No other characters
besides numbers and lowercase letters are permitted.

    ~zod/try=> `@t`~.foo
    'foo'
    ~zod/try=> `@t`~.foo.bar
    'foo.bar'
    ~zod/try=> `@t`~.foo~~bar
    'foo~bar'
    ~zod/try=> `@t`~.foo~-bar
    'foo_bar'
    ~zod/try=> `@t`~.foo-bar
    'foo-bar'

---

###spot

    ++  spot  ,[p=path q=pint]                              ::  range in file
  
Stack trace line.
  
    ~zod/try=> :into /=main=/bin/fail/hoon '!:  !!'
    + /~zod/main/359/bin/fail/hoon
    ~zod/try=> :fail
    ! /~zod/main/~2014.9.22..18.40.56..ef04/bin/fail/:<[1 5].[1 7]>
    ! exit


---

###tang

    ++  tang  (list tank)                                   ::  general error
    
Unused XX

---

###tank

    ++  tank  $%  [%leaf p=tape]                            ::  printing formats
                  [%palm p=[p=tape q=tape r=tape s=tape] q=(list tank)]
                  [%rose p=[p=tape q=tape r=tape] q=(list tank)]
              ==                                            ::
              
Pretty-printing structure: leaf nodes, XX, and surrounded-and-delimited tanks

    ~zod/try=> >(bex 20) (bex 19)<
    [%rose p=[p=" " q="[" r="]"] q=~[[%leaf p="1.048.576"] [%leaf p="524.288"]]]
    ~zod/try=> (wash [0 80] >(bex 20) (bex 19)<)
    <<"[1.048.576 524.288]">>
    ~zod/try=> (wash [0 15] >(bex 20) (bex 19)<)
    <<"[ 1.048.576" "  524.288" "]">>
    
    ~zod/try=> [(bex 150) (bex 151)]
    [ 1.427.247.692.705.959.881.058.285.969.449.495.136.382.746.624
      2.854.495.385.411.919.762.116.571.938.898.990.272.765.493.248
    ]


---

###tape

    ++  tape  (list char)                                   ::  like a string

One of Hoon's two string types (the other being `++cord`). A tape is a
list of chars.

    ~zod/try=> `(list ,char)`"foobar"
    "foobar"
    ~zod/try=> `(list ,@)`"foobar"
    ~[102 111 111 98 97 114]

---

###term

A restricted text atom for Hoon constants. The only characters
permitted are lowercase ASCII, - except as the first or last character,
and 0-9 except as the first character.

The syntax for `@tas` is the text itself, always preceded by `%`. This
means a term is always cubical. You can cast it to `@tas` if you like,
but we just about always want the cube:

    ~zod/try=> %dead-fish9
    %dead-fish9
    ~zod/try=> -:!>(%dead-fish9)
    [%cube p=271.101.667.197.767.630.546.276 q=[%atom p=%tas]]

The empty `@tas` has a special syntax, `$`:

    ~zod/try=> %$
    %$
    
A term without `%` is not a constant, but a name:

    ~zod/try=> dead-fish9
    ! -find-limb.dead-fish9
    ! find-none
    ! exit

---

###tiki

    ++  tiki                                                ::  test case
              $%  [& p=(unit term) q=wing]                  ::  simple wing
                  [| p=(unit term) q=twig]                  ::  named wing
              ==    
              
A `wing` or `twig` 

    ~zod/try=> (ream '=+  a=4  ?-(a @ ~)')
    [ %tsls
      p=[%ktts p=p=%a q=[%dtzy p=%ud q=4]]
        q
      [ %wthz
        p=[%.y p=~ q=~[%a]] 
        q=~[[p=[%axil p=[%atom p=~.]] q=[%bczp p=%null]]]
      ]
    ]
    ~zod/try=> (ream '=+  a=4  ?-(4 @ ~)')
    [ %tsls
      p=[%ktts p=p=%a q=[%dtzy p=%ud q=4]]
        q
      [ %wthz
        p=[%.n p=~ q=[%dtzy p=%ud q=4]]
        q=~[[p=[%axil p=[%atom p=~.]] q=[%bczp p=%null]]]
      ]
    ]


---

###tile

    ++  tile  

See the Tile section of the Hoon Reference.

---

###toga

    ++  toga                                                ::  face control
              $|  p=term                                    ::  two togas
              $%  [0 ~]                                     ::  no toga
                  [1 p=term q=toga]                         ::  deep toga
                  [2 p=toga q=toga]                         ::  cell toga
              ==                                            ::

A face, or tree of faces

    ~zod/try=> [a b]=[1 2 3]
    [a=1 b=[2 3]]
    ~zod/try=> (ream '[a b]=[1 2 3]')
    [ %ktts
      p=[%2 p=p=%a q=p=%b]
      q=[%cltr p=~[[%dtzy p=%ud q=1] [%dtzy p=%ud q=2] [%dtzy p=%ud q=3]]]
    ]
    
    ~zod/try=> [a ~]=[1 2 3]
    [a=1 2 3]
    ~zod/try=> (ream '[a ~]=[1 2 3]')
    [ %ktts
      p=[%2 p=p=%a q=[%0 ~]]
      q=[%cltr p=~[[%dtzy p=%ud q=1] [%dtzy p=%ud q=2] [%dtzy p=%ud q=3]]]
    ]

---

###trap

    ++  trap  ,_|.(_*)                                      ::  makes perfect sense
    
A trap is a core with one arm `++$`

    ~zod/try=> *trap
    <1.mws 101.jzo 1.ypj %164>
    ~zod/try=> (*trap)
    0

---

###trel

    ++  trel  |*  [a=$+(* *) b=$+(* *) c=$+(* *)]           ::  just a triple
              ,[p=a q=b r=c]                                ::

Tile constructor: three items.

    ~zod/try=> *(trel ,@ud ,@t ,@s)
    [p=0 q='' r=--0]
    
---

###tuna

    ++  tuna                                                ::  tagflow
              $%  [%a p=twig]                               ::  plain text
                  [%b p=twig]                               ::  single tag
                  [%c p=twig]                               ::  simple list
                  [%d p=twig]                               ::  dynamic list
                  [%e p=twig q=(list tuna)]                 ::  element
                  [%f p=(list tuna)]                        ::  subflow
              ==                                            ::
    
An XML template tree.  

Leaf %a contains plain-text, %b an empty tag, %c a static list, %d a 
dynamic list, %e a full node element containing a twig and a list of
tuna, and %f is a empty node.

---

###twig

++  twig

See Twig section of Hoon reference

---

###tine

    ++  tine  (list ,[p=tile q=twig])                       ::

Switch statement case

    ~zod/try=> (ream '?-(!! @ |, ^ &)')
    [ %wthz
      p=[%.n p=~ q=[%zpzp ~]]
        q
      ~[
        [p=[%axil p=[%atom p=~.]] q=[%dtzy p=%f q=1]]
        [p=[%axil p=%cell] q=[%dtzy p=%f q=0]]
      ]
    ]
    ~zod/try=> (tine |2:(ream '?-(!! @ |, ^ &)'))
    ~[
      [p=[%axil p=[%atom p=~.]] q=[%dtzy p=%f q=1]]
      [p=[%axil p=%cell] q=[%dtzy p=%f q=0]]
    ]

---

###tusk

    ++  tusk  (list twig)                                   ::
    
Variable-arity expression list.

    ~zod/try=> (ream '[1 2 3]')
    [%cltr p=~[[%dtzy p=%ud q=1] [%dtzy p=%ud q=2] [%dtzy p=%ud q=3]]]
    ~zod/try=> (tusk +:(ream '[1 2 3]'))
    ~[[%dtzy p=%ud q=1] [%dtzy p=%ud q=2] [%dtzy p=%ud q=3]]

---

###tyre

    ++  tyre  (list ,[p=term q=twig])                       ::
    
Assciative list of term to twig, used in jet hint processing.



---

###tyke

    ++  tyke  (list (unit twig))                            ::

List of twigs or gaps left to be inferred, in path parsing. 

    ~zod/try=> (scan "/==as=" porc:vast)
    [0 ~[~ ~ [~ [%dtzy p=%tas q=29.537]] ~]]
    ~zod/try=> `tyke`+:(scan "/==as=" porc:vast)
    ~[~ ~ [~ [%dtzy p=%tas q=29.537]] ~]

---

###tram

    ++  tram  (list ,[p=wing q=twig])                       ::
    
List of changes by location in context

    ~zod/try=> (ream '$(a 1, b 2)')
    [ %cnts
      p=~[%$]
      q=~[[p=~[%a] q=[%dtzy p=%ud q=1]] [p=~[%b] q=[%dtzy p=%ud q=2]]]
    ]
    ~zod/try=> (tram +:(ream '$(a 1, b 2)'))
    ~[[p=~ q=[% p=0]] [p=~[%a] q=[%dtzy p=%ud q=1]] [p=~[%b] q=[%dtzy p=%ud q=2]]]


---

###tone

    ++  tone  $%  [%0 p=*]                                  ::
                  [%1 p=(list)]                             ::
                  [%2 p=(list ,[@ta *])]                    ::
              ==                                            ::

Intermediate computation result: like `toon`, but stack trace
has not been rendered

    ~zod/try=> (mink [[20 21] 0 3] ,~)
    [%0 p=21]
    
    ~zod/try=> (mink [[0] !=(.^(cy//=main/1))] ,~)
    [%1 p=~[[31.075 1.685.027.454 1.852.399.981 49 0]]]
    ~zod/try=> (path [31.075 1.685.027.454 1.852.399.981 49 0])
    /cy/~zod/main/1
    
    ~zod/try=> (mink [[1 2] !=(~|(%hi +(.)))] ,~)
    [%2 p=~[[~.yelp 26.984]]]
    ~zod/try=> (mink [[1 2] !=(!:(+(.)))] ,~)
    [ %2
        p
      ~[
        [ ~.spot
          [ [ 1.685.027.454
              7.959.156
              \/159.445.990.350.374.058.574.398.238.344.143.957.205.628.479.572.65\/
                8.112.403.878.526
              \/                                                                  \/
              0
            ]
            [1 20]
            1
            24
          ]
        ]
      ]
    ]


---

###nock

    ++  nock  $&  [p=nock q=nock]                           ::  autocons
              $%  [%0 p=@]                                  ::  axis select
                  [%1 p=*]                                  ::  constant
                  [%2 p=nock q=nock]                        ::  compose
                  [%3 p=nock]                               ::  cell test
                  [%4 p=nock]                               ::  increment
                  [%5 p=nock q=nock]                        ::  equality test
                  [%6 p=nock q=nock r=nock]                 ::  if, then, else
                  [%7 p=nock q=nock]                        ::  serial compose
                  [%8 p=nock q=nock]                        ::  push onto subject
                  [%9 p=@ q=nock]                           ::  select arm and fire
                  [%10 p=?(@ [p=@ q=nock]) q=nock]          ::  hint
                  [%11 p=nock]                              ::  grab data from sky
              ==                                            ::

See [**Nock**] doc

    ~zod/try=> !=([+(.) 20 -<])
    [[4 0 1] [1 20] 0 4]
    ~zod/try=> (nock !=([+(.) 20]))
    [p=[%4 p=[%0 p=1]] q=[%1 p=20]]

---

###toon

    ++  toon  $%  [%0 p=*]                                  ::
                  [%1 p=(list)]                             ::
                  [%2 p=(list tank)]                        ::
              ==                                            ::
              
Nock computation result: success, block with list of requests blocked on,
or failure with stack trace.

    ~zod/try=> (mock [[20 21] 0 3] ,~)
    [%0 p=21]
    
    ~zod/try=> (mock [[0] !=(.^(cy//=main/1))] ,~)
    [%1 p=~[[31.075 1.685.027.454 1.852.399.981 49 0]]]
    ~zod/try=> (path [31.075 1.685.027.454 1.852.399.981 49 0])
    /cy/~zod/main/1
    
    ~zod/try=> (mock [[1 2] !=(!:(+(.)))] ,~)
    [%2 p=~[[%leaf p="/~zod/try/~2014.9.23..18.34.32..d3c5/:<[1 20].[1 24]>"]]]

---

###tune

    ++  tune  $%  [%0 p=vase]                               ::
                  [%1 p=(list)]                             ::
                  [%2 p=(list ,[@ta *])]                    ::
              ==                                            ::

---

###twin

    ++  twin  ,[p=term q=wing r=axis s=type]                ::

---

###type

See Type section of Hoon reference

---

###typo

    ++  typo  type                                          ::  old type
    
Type being imported from old arvo version on `%vega`

---

###udal

    ++  udal                                                ::  atomic change (%b)
              $:  p=@ud                                     ::  blockwidth
                  q=(list ,[p=@ud q=(unit ,[p=@ q=@])])     ::  indels
              ==                                            ::

---

###udon

    ++  udon                                                ::  abstract delta
              $:  p=umph                                    ::  preprocessor
                  $=  q                                     ::  patch
                  $%  [%a p=* q=*]                          ::  trivial replace
                      [%b p=udal]                           ::  atomic indel
                      [%c p=(urge)]                         ::  list indel
                      [%d p=upas q=upas]                    ::  tree edit
                  ==                                        ::
              ==                                            ::

---

###umph

    ++  umph                                                ::  change filter
              $|  $?  %a                                    ::  no filter
                      %b                                    ::  jamfile
                      %c                                    ::  LF text
                  ==                                        ::
              $%  [%d p=@ud]                                ::  blocklist
              ==                                            ::
    
---

###unce

    ++  unce  |*  a=_,*                                     ::  change part
              $%  [%& p=@ud]                                ::  skip[copy]
                  [%| p=(list a) q=(list a)]                ::  p -> q[chunk]
              ==                                            ::

---

###unit

    ++  unit  |*  a=_,*                                     ::  maybe
              $|(~ [~ u=a])                                 ::

---

###upas

    ++  upas                                                ::  tree change (%d)
              $&  [p=upas q=upas]                           ::  cell
              $%  [%0 p=axis]                               ::  copy old
                  [%1 p=*]                                  ::  insert new
                  [%2 p=axis q=udon]                        ::  mutate!
              ==                                            ::

---

###urge

    ++  urge  |*(a=_,* (list (unce a)))                     ::  list change

---

###vase

    ++  vase  ,[p=type q=*]                                 ::  type-value pair

---

###vise

    ++  vise  ,[p=typo q=*]                                 ::  old vase

---

###wall

    ++  wall  (list tape)                                   ::  text lines (no \n)

---

###wain

    ++  wain  (list cord)                                   ::  text lines (no \n)

---

###wing

    ++  wing  (list limb)                                   ::

---

###wine

    ++  wine  $|  ?(%noun %path %tank %void %wall %wool %yarn)
              $%  [%atom p=term]                            ::
                  [%core p=(list ,@ta) q=wine]              ::
                  [%face p=term q=wine]                     ::
                  [%list p=term q=wine]                     ::
                  [%pear p=term q=@]                        ::
                  [%pick p=(list wine)]                     ::
                  [%plot p=(list wine)]                     ::
                  [%stop p=@ud]                             ::
                  [%tree p=term q=wine]                     ::
                  [%unit p=term q=wine]                     ::
              ==                                            ::

---

###wonk

    ++  wonk  |*(veq=edge ?~(q.veq !! p.u.q.veq))           ::

---

###map

    ++  map  |*  [a=_,* b=_,*]                              ::  associative array
             $|(~ [n=[p=a q=b] l=(map a b) r=(map a b)])    ::

---

###qeu

    ++  qeu  |*  a=_,*                                      ::  queue
             $|(~ [n=a l=(qeu a) r=(qeu a)])                ::

---

###set

    ++  set  |*  a=_,*                                      ::  set
             $|(~ [n=a l=(set a) r=(set a)])                ::

---

###jar

    ++  jar  |*([a=_,* b=_,*] (map a (list b)))             ::  map of lists

---

###jug

    ++  jug  |*([a=_,* b=_,*] (map a (set b)))              ::  map of sets

---
